apply plugin: 'java'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

configurations {
    quasar
}

ext.quasarVersion = '0.7.3'
ext.log4j2Version = '2.4.1'

dependencies {
    compile project(':client')

    compile group: 'co.paralleluniverse', name: 'quasar-core', version: quasarVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j2Version
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4j2Version

    testCompile group: 'junit', name: 'junit', version: '4.12'

    quasar group: 'co.paralleluniverse', name: 'quasar-core', version: quasarVersion
}

classes {
    doFirst {
        ant.taskdef(name: 'scanSuspendables',
                classname: 'co.paralleluniverse.fibers.instrument.SuspendablesScanner',
                classpath: "build/classes/main:build/resources/main:${configurations.runtime.asPath}")
        ant.scanSuspendables(
                auto: true,
                suspendablesFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendables",
                supersFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendable-supers",
                append: true) {
            fileset(dir: sourceSets.main.output.classesDir)
        }
    }
}

task run(type: JavaExec) {
    main = 'com.github.lpedrosa.Application'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs "-javaagent:${configurations.quasar.iterator().next()}"
    systemProperties 'co.paralleluniverse.fibers.verifyInstrumentation': "true"
}
